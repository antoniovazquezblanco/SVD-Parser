plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.palantir.git-version' version '3.0.0'
}

group = 'io.github.antoniovazquezblanco'
version gitVersion()

repositories {
	mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
    
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
	publications {
        svdparser(MavenPublication) {
            from components.java
        }
    }
	repositories {
		maven {
			name = "OSSRH"
			url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
		    }
		}
		maven {
			name = "GitHub"
			url = "https://maven.pkg.github.com/antoniovazquezblanco/SVD-Parser"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}
