plugins {
    id 'signing'
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.34.0'
    id 'com.palantir.git-version' version '4.0.0'
}

group = 'io.github.antoniovazquezblanco'
version gitVersion()

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

mavenPublishing {
    coordinates(project.group, "svd-parser", project.version)
    pom {
        name = 'SVD Parser'
        description = 'A Java library to parse CMSIS SVD files.'
        url = 'https://github.com/antoniovazquezblanco/SVD-Parser'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'antoniovazquezblanco'
                name = 'Antonio VÃ¡zquez Blanco'
                email = 'antoniovazquezblanco@gmail.com'
            }
        }
        scm {
            connection = 'scm:git:https://github.com/antoniovazquezblanco/SVD-Parser.git'
            developerConnection = 'scm:git:git@github.com:antoniovazquezblanco/SVD-Parser.git'
            url = 'https://github.com/antoniovazquezblanco/SVD-Parser'
        }
    }
    signAllPublications()
    publishToMavenCentral()
}
