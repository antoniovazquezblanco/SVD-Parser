plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'com.palantir.git-version' version '3.2.0'
}

group = 'io.github.antoniovazquezblanco'
version gitVersion()

repositories {
	mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
    
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

signing {
    useInMemoryPgpKeys(System.getenv("OSSRH_GPG_SECRET_KEY"), System.getenv("OSSRH_GPG_SECRET_KEY_PASSWORD"))
    sign publishing.publications
}

publishing {
	publications {
        svdparser(MavenPublication) {
            from components.java
            pom {
                name = 'SVD Parser'
                description = ' A Java library to parse CMSIS SVD files.'
                url = 'https://github.com/antoniovazquezblanco/SVD-Parser'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'antoniovazquezblanco'
                        name = 'Antonio VÃ¡zquez Blanco'
                        email = 'antoniovazquezblanco@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/antoniovazquezblanco/SVD-Parser.git'
                    developerConnection = 'scm:git:git@github.com:antoniovazquezblanco/SVD-Parser.git'
                    url = 'https://github.com/antoniovazquezblanco/SVD-Parser'
                }
            }
        }
    }
	repositories {
		maven {
			name = "OSSRH"
			url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = System.getenv("OSSRH_USERNAME")
				password = System.getenv("OSSRH_PASSWORD")
		    }
		}
		maven {
			name = "GitHub"
			url = "https://maven.pkg.github.com/antoniovazquezblanco/SVD-Parser"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}
